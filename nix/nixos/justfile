nixos_directory := justfile_directory() / 'nix/nixos'

nixos_checker := if nixtype == "nixos" { "Y" } else { "N" }

# Check $NIXTYPE is set correctly for nixos
[group('nixos')]
nixos_check:
    @echo "{{BOLD + BLUE}}Is nixos set in \$NIXTYPE? {{if nixos_checker == "Y" { "OK!" } else { error("nixos not active") } }}{{NORMAL}}"

# Switch / rebuild nixos configuration (with updated kernel or boot params ; applies changes only on next boot)
[group('nixos')]
nixos_build_boot: nixos_check
    sudo -H nixos-rebuild boot --flake {{nixos_directory}}#{{nixconfig}}

# Switch / rebuild nixos configuration
[group('nixos')]
nixos_build: nixos_check
    sudo -H nixos-rebuild switch --flake {{nixos_directory}}#{{nixconfig}}

[group('nixos')]
nixos_update: nixos_check
    sudo nix flake update --flake {{nixos_directory}}

[group('nixos')]
nixos_upgrade: nixos_check
    sudo -H nix-channel --update
    sudo -H nix profile upgrade --all

[group('nixos')]
nixos_lts-channel: nixos_check
    sudo -H nix-channel --remove nixpkgs
    sudo -H nix-channel --add https://nixos.org/channels/nixos-{{nixpkgs}} nixpkgs
    sudo -H nix-channel --update

[group('nixos')]
nixos_unstable-channel: nixos_check
    sudo -H nix-channel --remove nixpkgs
    sudo -H nix-channel --add https://nixos.org/channels/nixos-unstable nixpkgs
    sudo -H nix-channel --update

[group('nixos')]
nixos_system-eval:
    @echo {{nixos_directory}}#nixosConfigurations.{{nixconfig}}.config

[group('nixos')]
nixos_home-eval:
    @echo {{nixos_directory}}#nixosConfigurations.{{nixconfig}}.config.home-manager.users.{{nixusername}}