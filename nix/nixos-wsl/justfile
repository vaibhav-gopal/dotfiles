wsl_directory := justfile_directory() / 'nix/nixos-wsl'

wsl_checker := if nixtype == "nixos-wsl" { "Y" } else { "N" }

# Check $NIXTYPE is set correctly for nixos
[group('nixos-wsl')]
nixos-wsl_check:
    @echo "{{BOLD + BLUE}}Is nixos-wsl set in \$NIXTYPE? {{if wsl_checker == "Y" { "OK!" } else { error("nixos-wsl not active") } }}{{NORMAL}}"

# Switch / rebuild nixos-wsl configuration
[group('nixos-wsl')]
nixos-wsl_build: nixos-wsl_check
    sudo -H nixos-rebuild switch --flake {{wsl_directory}}#{{nixconfig}}

[group('nixos-wsl')]
nixos-wsl_update: nixos-wsl_check
    sudo nix flake update --flake {{wsl_directory}}

[group('nixos-wsl')]
nixos-wsl_upgrade: nixos-wsl_check
    sudo -H nix-channel --update
    sudo -H nix profile upgrade --all

[group('nixos-wsl')]
nixos-wsl_lts-channel: nixos-wsl_check
    sudo -H nix-channel --remove nixpkgs
    sudo -H nix-channel --add https://nixos.org/channels/nixos-{{nixpkgs}} nixpkgs
    sudo -H nix-channel --update

[group('nixos-wsl')]
nixos-wsl_unstable-channel: nixos-wsl_check
    sudo -H nix-channel --remove nixpkgs
    sudo -H nix-channel --add https://nixos.org/channels/nixos-unstable nixpkgs
    sudo -H nix-channel --update

[group('nixos-wsl')]
nixos-wsl_arch-features: nixos-wsl_check
    sudo nix eval --json {{wsl_directory}}#nixosConfigurations.{{nixconfig}}.config.features

[group('nixos-wsl')]
nixos-wsl_home-features: nixos-wsl_check
    sudo nix eval --json {{wsl_directory}}#nixosConfigurations.{{nixconfig}}.config.home-manager.users.{{nixusername}}.features

[group('nixos-wsl')]
nixos-wsl_system-features: nixos-wsl_check
    sudo nix eval --json {{wsl_directory}}#nixosConfigurations.{{nixconfig}}.config.home-manager.users.{{nixusername}}.system.features
