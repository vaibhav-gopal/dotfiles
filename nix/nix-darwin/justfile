darwin_directory := justfile_directory() / 'nix/nix-darwin'

darwin_checker := if nixtype == "nix-darwin" { "Y" } else { "N" }

# Check $NIXTYPE is set correctly for nix-darwin
[group('nix-darwin')]
nix-darwin_check:
    @echo "{{BOLD + BLUE}}Is nix-darwin set in \$NIXTYPE? {{if darwin_checker == "Y" { "OK!" } else { error("nix-darwin not active") } }}{{NORMAL}}"

# First time init nix-darwin
[group('nix-darwin')]
nix-darwin_init: nix-darwin_check
    sudo -H nix --extra-experimental-features "nix-command flakes" run nix-darwin/nix-darwin-{{nixpkgs}}#darwin-rebuild -- switch --flake {{darwin_directory}}#{{nixconfig}}

# Switch / rebuild nix-darwin configuration
[group('nix-darwin')]
nix-darwin_build: nix-darwin_check
    sudo -H darwin-rebuild switch --flake {{darwin_directory}}#{{nixconfig}}

[group('nix-darwin')]
nix-darwin_update: nix-darwin_check
    sudo nix flake update --flake {{darwin_directory}}

[group('nix-darwin')]
nix-darwin_upgrade: nix-darwin_check
    sudo -H nix-channel --update
    sudo -H nix profile upgrade --all

[group('nix-darwin')]
nix-darwin_lts-channel: nix-darwin_check
    sudo -H nix-channel --remove nixpkgs
    sudo -H nix-channel --add https://nixos.org/channels/nixpkgs-{{nixpkgs}}-darwin nixpkgs
    sudo -H nix-channel --update

[group('nix-darwin')]
nix-darwin_unstable-channel: nix-darwin_check
    sudo -H nix-channel --remove nixpkgs
    sudo -H nix-channel --add https://nixos.org/channels/nixpkgs-unstable nixpkgs
    sudo -H nix-channel --update

[group('nix-darwin')]
nix-darwin_arch-features: nix-darwin_check
    sudo nix eval --json {{darwin_directory}}#darwinConfigurations.{{nixconfig}}.config.features

[group('nix-darwin')]
nix-darwin_home-features: nix-darwin_check
    sudo nix eval --json {{darwin_directory}}#darwinConfigurations.{{nixconfig}}.config.home-manager.users.{{nixusername}}.features

[group('nix-darwin')]
nix-darwin_system-features: nix-darwin_check
    sudo nix eval --json {{darwin_directory}}#darwinConfigurations.{{nixconfig}}.config.home-manager.users.{{nixusername}}.system.features